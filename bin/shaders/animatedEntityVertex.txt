#version 150

const int MAX_JOINTS = 50;
const int MAX_WEIGHTS = 3;

in vec3 in_position;
in vec2 in_textureCoords;
in vec3 in_normal;
in ivec3 in_jointIndices;
in vec3 in_weights;

out vec2 pass_textureCoords;
out vec3 pass_normal;

out vec3 toLightVector[4];

uniform mat4 jointTransforms[MAX_JOINTS];
uniform mat4 projectionViewMatrix;

uniform mat4 transformationMatrix;

uniform vec3 lightPosition[4];

void main(void){
	
	vec4 totalLocalPos = vec4(0.0);
	vec4 totalNormal = vec4(0.0);
	for(int i=0;i<MAX_WEIGHTS;i++){
		vec4 localPosition = jointTransforms[in_jointIndices[i]] * vec4(in_position, 1.0);
		totalLocalPos += localPosition * in_weights[i];
		
		vec4 worldNormal = jointTransforms[in_jointIndices[i]] * vec4(in_normal, 0.0);
		totalNormal += worldNormal * in_weights[i];
	}
	
	
	for(int i=0;i<4;i++){
		toLightVector[i] = lightPosition[i];
	}
	
	
	totalLocalPos = transformationMatrix * totalLocalPos;

	gl_Position = projectionViewMatrix * totalLocalPos;
	pass_normal = totalNormal.xyz;
	pass_textureCoords = in_textureCoords;

}